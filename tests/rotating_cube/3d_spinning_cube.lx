@module "main"

@use "math" as math
@use "memory" as mem
@use "string" as string
@use "termfx" as fx
@use "time" as time
@use "io" as io

let zBuffer: *double;
let _colorBuffer: **char;
let _buffer: *char;

let cube_width: double;
const width: int = 160;
const height: int = 50;

const distance_from_cam: int = 30;

const increment_speed: double = 0.6;
let horizontal_offset: double;
const k1: double = 40.0;

const calculateX -> fn (i: int, j: int, k: int, A: double, B: double, C: double) double {
  let sA: double = math::sin(A);
  let cA: double = math::cos(A);
  let sB: double = math::sin(B);
  let cB: double = math::cos(B);
  let sC: double = math::sin(C);
  let cC: double = math::cos(C);

  return j * sA * sB * cC - k * cA * sB * cC + 
         j * cA * sC + k * sA * sC + i * cB * cC;
}

const calculateY -> fn (i: int, j: int, k: int, A: double, B: double, C: double) double {
  let sA: double = math::sin(A);
  let cA: double = math::cos(A);
  let sB: double = math::sin(B);
  let cB: double = math::cos(B);
  let sC: double = math::sin(C);
  let cC: double = math::cos(C);

  return j * cA * cC + k * sA * cC - 
         j * sA * sB * sC + k * cA * sB * sC - 
         i * cB * sC;
}

const calculateZ -> fn (i: int, j: int, k: int, A: double, B: double) double {
  let sA: double = math::sin(A);
  let cA: double = math::cos(A);
  let sB: double = math::sin(B);
  let cB: double = math::cos(B);
  
  return k * cA * cB - j * sA * cB + i * sB;
}

const calculateForSurface -> fn (cX: double, cY: double, cZ: double,
                                ch: char, color: *char,
                                A: double, B: double, C: double) void {
  let x: double = calculateX(cast<int>(cX), cast<int>(cY), cast<int>(cZ), A, B, C);
  let y: double = calculateY(cast<int>(cX), cast<int>(cY), cast<int>(cZ), A, B, C);
  let z: double = calculateZ(cast<int>(cX), cast<int>(cY), cast<int>(cZ), A, B) + distance_from_cam;

  if (z <= 0.001) { return; }
  
  let ooz: double = 1.0 / z;
  if (ooz > 1000.0 || ooz < -1000.0) { return; }

  let xp: int = cast<int>(width / 2 + horizontal_offset + k1 * ooz * x * 2);
  let yp: int = cast<int>(height / 2 + k1 * ooz * y);

  if (xp >= 0 && xp < width && yp >= 0 && yp < height) {
    let idx: int = xp + yp * width;
    if (ooz > zBuffer[idx]) {
      zBuffer[idx] = ooz;
      _buffer[idx] = ch;
      _colorBuffer[idx] = color;
    }
  }
}


const print_cube -> fn (A: double, B: double, C: double) void {
  loop [cubeX: double = -cube_width](cubeX < cube_width) : (cubeX = cubeX + increment_speed) {
    loop [cubeY: double = -cube_width](cubeY < cube_width) : (cubeY = cubeY + increment_speed) {
      calculateForSurface(cubeX,       cubeY,       -cube_width, '@', fx::BRIGHT_RED,     A, B, C);
      calculateForSurface(cube_width,  cubeY,       cubeX,       '+', fx::BRIGHT_GREEN,   A, B, C);
      calculateForSurface(-cube_width, cubeY,       -cubeX,      '~', fx::BRIGHT_YELLOW,  A, B, C);
      calculateForSurface(-cubeX,      cubeY,       cube_width,  '#', fx::BRIGHT_BLUE,    A, B, C);
      calculateForSurface(cubeX,       -cube_width, -cubeY,      '*', fx::BRIGHT_CYAN,    A, B, C);
      calculateForSurface(cubeX,       cube_width,  cubeY,       '-', fx::BRIGHT_MAGENTA, A, B, C);
    }
  }
}


pub const main -> fn () int {
  zBuffer = cast<*double>(alloc(width * height * sizeof<double>));
  _colorBuffer = cast<**char>(alloc(width * height * 8));
  _buffer = cast<*char>(alloc(width * height));
  
  defer { free(zBuffer); free(_colorBuffer); free(_buffer); }

  io::print_str("%s%s%s3D%s%s Spinning Cubes in Luma%s%s\n", 
                [fx::CLEAR_SCREEN, fx::CURSOR_HOME, fx::BRIGHT_MAGENTA, 
                 fx::RESET, fx::BRIGHT_CYAN, fx::RESET, fx::CURSOR_HIDE]);

  let frame_count: int = 0;

  let A: double = 1.2;
  let B: double = 0.8;
  let C: double = 0.4;

  loop {
    mem::memset(cast<*void>(_buffer),     32, width * height);
    mem::memset(cast<*void>(_colorBuffer), 0, width * height * 8);
    mem::memset(cast<*void>(zBuffer),      0, width * height * sizeof<double>);
    
    // cube_width = 20.0;
    // horizontal_offset = -2 * cube_width;
    // print_cube(A, B, C);
    
    cube_width = 10.0;
    horizontal_offset = -15.0;
    print_cube(A, B, C);  // Cube 1

    cube_width = 5.0;
    horizontal_offset = -15.0;
    print_cube(-A, -B, C * 1.5);

    cube_width = 2.5;
    horizontal_offset = -15.0;
    print_cube(-A, B, -C * 4.5);

    output(fx::CURSOR_HOME);
    loop [k: int = 0](k < width * height) : (++k) {
      if (k % width == 0) { output("\n"); }

      if (_colorBuffer[k] != cast<*char>(0)) {
        output(_colorBuffer[k]);
      }
      output(string::from_char(_buffer[k]), fx::RESET);
    }
    
    output( 
      fx::move_cursor(1, 29),
      fx::BOLD, fx::WHITE, " Frame: ",
      fx::BRIGHT_GREEN, frame_count, " ",
      fx::RESET
    );

    // Rotate cube
    A = (A + 0.05) % math::TWO_PI;
    B = (B + 0.05) % math::TWO_PI;
    C = (C + 0.01) % math::TWO_PI;

    // Increment frame count
    ++frame_count;

    time::usleep(20000);
  }

  return 0;
}
